# ===================================================================================
#  The Irrlicht CMake configuration file
#
#             ** File generated automatically, do not modify **
#
#  Usage from an external project:
#    In your CMakeLists.txt, add these lines:
#
#    find_package(Irrlicht REQUIRED)
#    include_directories(${Irrlicht_INCLUDE_DIRS}) # Not needed for CMake >= 2.8.11
#    target_link_libraries(MY_TARGET_NAME ${Irrlicht_LIBS})
#
#    Or you can search for specific Irrlicht modules:
#
#    find_package(Irrlicht REQUIRED core videoio)
#
#    You can also mark Irrlicht components as optional:

#    find_package(Irrlicht REQUIRED core OPTIONAL_COMPONENTS viz)
#
#    If the module is found then IRRLICHT_<MODULE>_FOUND is set to TRUE.
#
#    This file will define the following variables:
#      - Irrlicht_LIBS                     : The list of all imported targets for Irrlicht modules.
#      - Irrlicht_INCLUDE_DIRS             : The Irrlicht include directories.
#      - Irrlicht_COMPUTE_CAPABILITIES     : The version of compute capability.
#      - Irrlicht_ANDROID_NATIVE_API_LEVEL : Minimum required level of Android API.
#      - Irrlicht_VERSION                  : The version of this Irrlicht build: "@OPENCV_VERSION_PLAIN@"
#      - Irrlicht_VERSION_MAJOR            : Major version part of Irrlicht_VERSION: "@OPENCV_VERSION_MAJOR@"
#      - Irrlicht_VERSION_MINOR            : Minor version part of Irrlicht_VERSION: "@OPENCV_VERSION_MINOR@"
#      - Irrlicht_VERSION_PATCH            : Patch version part of Irrlicht_VERSION: "@OPENCV_VERSION_PATCH@"
#      - Irrlicht_VERSION_STATUS           : Development status of this build: "@OPENCV_VERSION_STATUS@"
#
#    Advanced variables:
#      - Irrlicht_SHARED                   : Use Irrlicht as shared library
#      - Irrlicht_INSTALL_PATH             : Irrlicht location
#      - Irrlicht_LIB_COMPONENTS           : Present Irrlicht modules list
#      - Irrlicht_USE_MANGLED_PATHS        : Mangled Irrlicht path flag
#
#    Deprecated variables:
#      - Irrlicht_VERSION_TWEAK            : Always "0"
#
# ===================================================================================

# ======================================================
#  Version variables:
# ======================================================
SET(Irrlicht_VERSION @OPENCV_VERSION_PLAIN@)
SET(Irrlicht_VERSION_MAJOR  @OPENCV_VERSION_MAJOR@)
SET(Irrlicht_VERSION_MINOR  @OPENCV_VERSION_MINOR@)
SET(Irrlicht_VERSION_PATCH  @OPENCV_VERSION_PATCH@)
SET(Irrlicht_VERSION_TWEAK  0)
SET(Irrlicht_VERSION_STATUS "@OPENCV_VERSION_STATUS@")

include(FindPackageHandleStandardArgs)

if(NOT CMAKE_VERSION VERSION_LESS 2.8.8
    AND Irrlicht_FIND_COMPONENTS  # prevent excessive output
)
  # HANDLE_COMPONENTS was introduced in CMake 2.8.8
  list(APPEND _Irrlicht_FPHSA_ARGS HANDLE_COMPONENTS)
  # The missing components will be handled by the FindPackageHandleStandardArgs
  # module.
  set(_Irrlicht_HANDLE_COMPONENTS_MANUALLY FALSE)
else()
  # The missing components will be handled by this config.
  set(_Irrlicht_HANDLE_COMPONENTS_MANUALLY TRUE)
endif()

# Extract directory name from full path of the file currently being processed.
# Note that CMake 2.8.3 introduced CMAKE_CURRENT_LIST_DIR. We reimplement it
# for older versions of CMake to support these as well.
if(CMAKE_VERSION VERSION_LESS "2.8.3")
  get_filename_component(CMAKE_CURRENT_LIST_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
endif()

# Extract the directory where *this* file has been installed (determined at cmake run-time)
# Get the absolute path with no ../.. relative marks, to eliminate implicit linker warnings
get_filename_component(Irrlicht_CONFIG_PATH "${CMAKE_CURRENT_LIST_DIR}" REALPATH)
get_filename_component(Irrlicht_INSTALL_PATH "${Irrlicht_CONFIG_PATH}/@Irrlicht_INSTALL_PATH_RELATIVE_CONFIGCMAKE@" REALPATH)

# Search packages for host system instead of packages for target system.
# in case of cross compilation this macro should be defined by toolchain file
if(NOT COMMAND find_host_package)
    macro(find_host_package)
        find_package(${ARGN})
    endmacro()
endif()
if(NOT COMMAND find_host_program)
    macro(find_host_program)
        find_program(${ARGN})
    endmacro()
endif()



@ANDROID_CONFIGCMAKE@



# Some additional settings are required if Irrlicht is built as static libs
set(Irrlicht_SHARED @BUILD_SHARED_LIBS@)

# Enables mangled install paths, that help with side by side installs
set(Irrlicht_USE_MANGLED_PATHS @Irrlicht_USE_MANGLED_PATHS_CONFIGCMAKE@)

set(Irrlicht_LIB_COMPONENTS @OPENCV_MODULES_CONFIGCMAKE@)
set(__Irrlicht_INCLUDE_DIRS @OpenCV_INCLUDE_DIRS_CONFIGCMAKE@)

set(Irrlicht_INCLUDE_DIRS "")
foreach(d ${__Irrlicht_INCLUDE_DIRS})
  get_filename_component(__d "${d}" REALPATH)
  if(NOT EXISTS "${__d}")
    if(NOT Irrlicht_FIND_QUIETLY)
      message(WARNING "Irrlicht: Include directory doesn't exist: '${d}'. Irrlicht installation may be broken. Skip...")
    endif()
  else()
    list(APPEND Irrlicht_INCLUDE_DIRS "${__d}")
  endif()
endforeach()
unset(__d)


if(NOT TARGET irrlicht_core)
  include(${CMAKE_CURRENT_LIST_DIR}/IrrlichtModules${Irrlicht_MODULES_SUFFIX}.cmake)
endif()

if(NOT CMAKE_VERSION VERSION_LESS "2.8.11")
  # Target property INTERFACE_INCLUDE_DIRECTORIES available since 2.8.11:
  # * http://www.cmake.org/cmake/help/v2.8.11/cmake.html#prop_tgt:INTERFACE_INCLUDE_DIRECTORIES
  foreach(__component ${Irrlicht_LIB_COMPONENTS})
    if(TARGET ${__component})
      set_target_properties(
          ${__component}
          PROPERTIES
          INTERFACE_INCLUDE_DIRECTORIES "${Irrlicht_INCLUDE_DIRS}"
      )
    endif()
  endforeach()
endif()


if(NOT DEFINED IRRLICHT_MAP_IMPORTED_CONFIG)
  if(CMAKE_GENERATOR MATCHES "Visual Studio" OR MSVC)
    # Irrlicht supports Debug and Release builds only.
    # But MSVS has 'RelWithDebInfo' 'MinSizeRel' configurations for applications.
    # By default CMake maps these configuration on the first available (Debug) which is wrong.
    # Non-Debug build of Application can't be used with Irrlicht Debug build (ABI mismatch problem)
    # Add mapping of RelWithDebInfo and MinSizeRel to Release here
    set(IRRLICHT_MAP_IMPORTED_CONFIG "RELWITHDEBINFO=!Release;MINSIZEREL=!Release")
  endif()
endif()
set(__remap_warnings "")
macro(ocv_map_imported_config target)
  if(DEFINED IRRLICHT_MAP_IMPORTED_CONFIG) # list, "RELWITHDEBINFO=Release;MINSIZEREL=Release"
    get_target_property(__available_configurations ${target} IMPORTED_CONFIGURATIONS)
    foreach(remap ${IRRLICHT_MAP_IMPORTED_CONFIG})
      if(remap MATCHES "^(.+)=(!?)([^!]+)$")
        set(__remap_config "${CMAKE_MATCH_1}")
        set(__final_config "${CMAKE_MATCH_3}")
        set(__force_flag "${CMAKE_MATCH_2}")
        string(TOUPPER "${__final_config}" __final_config_upper)
        string(TOUPPER "${__remap_config}" __remap_config_upper)
        if(";${__available_configurations};" MATCHES ";${__remap_config_upper};" AND NOT "${__force_flag}" STREQUAL "!")
          # configuration already exists, skip remap
          set(__remap_warnings "${__remap_warnings}... Configuration already exists ${__remap_config} (skip mapping ${__remap_config} => ${__final_config}) (available configurations: ${__available_configurations})\n")
          continue()
        endif()
        if(__available_configurations AND NOT ";${__available_configurations};" MATCHES ";${__final_config_upper};")
          # skip, configuration is not available
          if(NOT "${__force_flag}" STREQUAL "!")
            set(__remap_warnings "${__remap_warnings}... Configuration is not available '${__final_config}' for ${target}, build may fail (available configurations: ${__available_configurations})\n")
          endif()
        endif()
        set_target_properties(${target} PROPERTIES
            MAP_IMPORTED_CONFIG_${__remap_config} "${__final_config}"
        )
      else()
        message(WARNING "Invalid entry of IRRLICHT_MAP_IMPORTED_CONFIG: '${remap}' (${IRRLICHT_MAP_IMPORTED_CONFIG})")
      endif()
    endforeach()
  endif()
endmacro()


# ==============================================================
#  Form list of modules (components) to find
# ==============================================================
if(NOT Irrlicht_FIND_COMPONENTS)
  set(Irrlicht_FIND_COMPONENTS ${Irrlicht_LIB_COMPONENTS})
  list(REMOVE_ITEM Irrlicht_FIND_COMPONENTS irrlicht_java)
  if(GTest_FOUND OR GTEST_FOUND)
    list(REMOVE_ITEM Irrlicht_FIND_COMPONENTS irrlicht_ts)
  endif()
endif()

set(Irrlicht_WORLD_COMPONENTS @OPENCV_WORLD_MODULES@)

# expand short module names and see if requested components exist
foreach(__cvcomponent ${Irrlicht_FIND_COMPONENTS})
  # Store the name of the original component so we can set the
  # Irrlicht_<component>_FOUND variable which can be checked by the user.
  set (__original_cvcomponent ${__cvcomponent})
  if(NOT __cvcomponent MATCHES "^irrlicht_")
    set(__cvcomponent irrlicht_${__cvcomponent})
  endif()
  list(FIND Irrlicht_LIB_COMPONENTS ${__cvcomponent} __cvcomponentIdx)
  if(__cvcomponentIdx LESS 0)
    if(_Irrlicht_HANDLE_COMPONENTS_MANUALLY)
      # Either the component is required or the user did not set any components at
      # all. In the latter case, the Irrlicht_FIND_REQUIRED_<component> variable
      # will not be defined since it is not set by this config. So let's assume
      # the implicitly set components are always required.
      if(NOT DEFINED Irrlicht_FIND_REQUIRED_${__original_cvcomponent} OR
          Irrlicht_FIND_REQUIRED_${__original_cvcomponent})
        message(FATAL_ERROR "${__cvcomponent} is required but was not found")
      elseif(NOT Irrlicht_FIND_QUIETLY)
        # The component was marked as optional using OPTIONAL_COMPONENTS
        message(WARNING "Optional component ${__cvcomponent} was not found")
      endif()
    endif(_Irrlicht_HANDLE_COMPONENTS_MANUALLY)
    #indicate that module is NOT found
    string(TOUPPER "${__cvcomponent}" __cvcomponentUP)
    set(${__cvcomponentUP}_FOUND "${__cvcomponentUP}_FOUND-NOTFOUND")
    set(Irrlicht_${__original_cvcomponent}_FOUND FALSE)
  else()
    # Not using list(APPEND) here, because Irrlicht_LIBS may not exist yet.
    # Also not clearing Irrlicht_LIBS anywhere, so that multiple calls
    # to find_package(Irrlicht) with different component lists add up.
    set(Irrlicht_LIBS ${Irrlicht_LIBS} "${__cvcomponent}")
    #indicate that module is found
    string(TOUPPER "${__cvcomponent}" __cvcomponentUP)
    set(${__cvcomponentUP}_FOUND 1)
    set(Irrlicht_${__original_cvcomponent}_FOUND TRUE)
  endif()
  if(Irrlicht_SHARED AND ";${Irrlicht_WORLD_COMPONENTS};" MATCHES ";${__cvcomponent};" AND NOT TARGET ${__cvcomponent})
    get_target_property(__implib_dbg irrlicht_world IMPORTED_IMPLIB_DEBUG)
    get_target_property(__implib_release irrlicht_world  IMPORTED_IMPLIB_RELEASE)
    get_target_property(__location_dbg irrlicht_world IMPORTED_LOCATION_DEBUG)
    get_target_property(__location_release irrlicht_world  IMPORTED_LOCATION_RELEASE)
    get_target_property(__include_dir irrlicht_world INTERFACE_INCLUDE_DIRECTORIES)
    add_library(${__cvcomponent} SHARED IMPORTED)
    set_target_properties(${__cvcomponent} PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${__include_dir}")
    if(__location_dbg)
      set_property(TARGET ${__cvcomponent} APPEND PROPERTY IMPORTED_CONFIGURATIONS DEBUG)
      set_target_properties(${__cvcomponent} PROPERTIES
        IMPORTED_IMPLIB_DEBUG "${__implib_dbg}"
        IMPORTED_LINK_INTERFACE_LIBRARIES_DEBUG ""
        IMPORTED_LOCATION_DEBUG "${__location_dbg}"
      )
    endif()
    if(__location_release)
      set_property(TARGET ${__cvcomponent} APPEND PROPERTY IMPORTED_CONFIGURATIONS RELEASE)
      set_target_properties(${__cvcomponent} PROPERTIES
        IMPORTED_IMPLIB_RELEASE "${__implib_release}"
        IMPORTED_LINK_INTERFACE_LIBRARIES_RELEASE ""
        IMPORTED_LOCATION_RELEASE "${__location_release}"
      )
    endif()
  endif()
  if(TARGET ${__cvcomponent})
    ocv_map_imported_config(${__cvcomponent})
  endif()
endforeach()

if(__remap_warnings AND NOT Irrlicht_FIND_QUIETLY)
  message("Irrlicht: configurations remap warnings:\n${__remap_warnings}Irrlicht: Check variable IRRLICHT_MAP_IMPORTED_CONFIG=${IRRLICHT_MAP_IMPORTED_CONFIG}")
endif()

# ==============================================================
# Compatibility stuff
# ==============================================================
set(Irrlicht_LIBRARIES ${Irrlicht_LIBS})

# Require C++11 features for Irrlicht modules
if(CMAKE_VERSION VERSION_LESS "3.1")
  if(NOT Irrlicht_FIND_QUIETLY AND NOT IRR_HIDE_WARNING_COMPILE_FEATURES)
    message(STATUS "Irrlicht: CMake version is low (${CMAKE_VERSION}, required 3.1+). Can't enable C++11 features")
  endif()
else()
  set(__target irrlicht_core)
  if(TARGET irrlicht_world)
    set(__target irrlicht_world)
  endif()
  set(__compile_features cxx_std_11)  # CMake 3.8+
  if(DEFINED IRR_COMPILE_FEATURES)
    set(__compile_features ${IRR_COMPILE_FEATURES})  # custom override
  elseif(CMAKE_VERSION VERSION_LESS "3.8")
    set(__compile_features cxx_auto_type cxx_rvalue_references cxx_lambdas)
  endif()
  if(__compile_features)
    # Simulate exported result of target_compile_features(irrlicht_core PUBLIC ...)
    set_target_properties(${__target} PROPERTIES
        INTERFACE_COMPILE_FEATURES "${__compile_features}"
    )
  endif()
  unset(__target)
  unset(__compile_features)
endif()

#
# Some macros for samples
#
macro(ocv_check_dependencies)
  set(OCV_DEPENDENCIES_FOUND TRUE)
  foreach(d ${ARGN})
    if(NOT TARGET ${d})
      message(WARNING "Irrlicht: Can't resolve dependency: ${d}")
      set(OCV_DEPENDENCIES_FOUND FALSE)
      break()
    endif()
  endforeach()
endmacro()

# adds include directories in such way that directories from the Irrlicht source tree go first
function(ocv_include_directories)
  set(__add_before "")
  file(TO_CMAKE_PATH "${Irrlicht_INSTALL_PATH}" __baseDir)
  foreach(dir ${ARGN})
    get_filename_component(__abs_dir "${dir}" ABSOLUTE)
    if("${__abs_dir}" MATCHES "^${__baseDir}")
      list(APPEND __add_before "${dir}")
    else()
      include_directories(AFTER SYSTEM "${dir}")
    endif()
  endforeach()
  include_directories(BEFORE ${__add_before})
endfunction()

macro(ocv_include_modules)
  include_directories(BEFORE "${Irrlicht_INCLUDE_DIRS}")
endmacro()

macro(ocv_include_modules_recurse)
  include_directories(BEFORE "${Irrlicht_INCLUDE_DIRS}")
endmacro()

macro(ocv_target_link_libraries)
  target_link_libraries(${ARGN})
endmacro()

# remove all matching elements from the list
macro(ocv_list_filterout lst regex)
  foreach(item ${${lst}})
    if(item MATCHES "${regex}")
      list(REMOVE_ITEM ${lst} "${item}")
    endif()
  endforeach()
endmacro()

# We do not actually need REQUIRED_VARS to be checked for. Just use the
# installation directory for the status.
find_package_handle_standard_args(Irrlicht REQUIRED_VARS Irrlicht_INSTALL_PATH
                                  VERSION_VAR Irrlicht_VERSION ${_Irrlicht_FPHSA_ARGS})
